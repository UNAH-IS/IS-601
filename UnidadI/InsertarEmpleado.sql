create or replace PROCEDURE P_INSERTAR_EMPLEADO(
    P_FIRST_NAME IN EMPLOYEES.FIRST_NAME%TYPE,
    P_LAST_NAME IN EMPLOYEES.LAST_NAME%TYPE,
    P_EMAIL EMPLOYEES.EMAIL%TYPE,
    P_PHONE_NUMBER EMPLOYEES.PHONE_NUMBER%TYPE,
    P_HIRE_DATE EMPLOYEES.HIRE_DATE%TYPE,
    P_JOB_ID EMPLOYEES.JOB_ID%TYPE,
    P_SALARY EMPLOYEES.SALARY%TYPE,
    P_COMMISSION_PCT EMPLOYEES.COMMISSION_PCT%TYPE,
    P_MANAGER_ID EMPLOYEES.MANAGER_ID%TYPE,
    P_DEPARTMENT_ID EMPLOYEES.DEPARTMENT_ID%TYPE,
    P_MENSAJE_RESULTADO OUT VARCHAR2,
    P_CODIGO_RESULTADO OUT NUMBER,
    P_CODIGO_EMPLEADO OUT EMPLOYEES.EMPLOYEE_ID%TYPE
) AS
    V_CANTIDAD_JOBS NUMBER;
    V_JOB_ID JOBS.JOB_ID%TYPE;
    V_MIN_SALARY JOBS.MIN_SALARY%TYPE;
    V_MAX_SALARY JOBS.MAX_SALARY%TYPE;
BEGIN
    BEGIN
        SELECT JOB_ID, MIN_SALARY, MAX_SALARY 
        INTO V_JOB_ID, V_MIN_SALARY, V_MAX_SALARY 
        FROM JOBS
        WHERE JOB_ID = P_JOB_ID;
    EXCEPTION
        WHEN NO_DATA_FOUND THEN
            NULL;
    END;
    IF (P_SALARY NOT BETWEEN V_MIN_SALARY AND V_MAX_SALARY) THEN 
        P_MENSAJE_RESULTADO := 'SALARIO FUERA DE RANGO';
        P_CODIGO_RESULTADO := 2;
        RETURN;
    END IF;
    IF (V_JOB_ID IS NOT NULL) THEN
        P_CODIGO_EMPLEADO := SEQ_EMPLOYEE_ID.NEXTVAL;
        INSERT INTO employees (
            employee_id,
            first_name,
            last_name,
            email,
            phone_number,
            hire_date,
            job_id,
            salary,
            commission_pct,
            manager_id,
            department_id
        ) VALUES (
            P_CODIGO_EMPLEADO,
            P_FIRST_NAME,
            P_LAST_NAME,
            P_EMAIL,
            P_PHONE_NUMBER,
            P_HIRE_DATE,
            P_JOB_ID,
            P_SALARY,
            P_COMMISSION_PCT,
            P_MANAGER_ID,
            P_DEPARTMENT_ID
        );
        COMMIT;
        P_MENSAJE_RESULTADO := 'REGISTRO ALMACENADO CON Ã‰XITO';
        P_CODIGO_RESULTADO := 0;
    ELSE 
        P_MENSAJE_RESULTADO := 'NO EXISTE EL JOB_ID INDICADO';
        P_CODIGO_RESULTADO := 1;
    END IF;
EXCEPTION
    WHEN OTHERS THEN
        P_MENSAJE_RESULTADO := 'ERROR AL INSERTAR' || SQLERRM || ' - ' || SQLCODE;
        P_CODIGO_RESULTADO := -1;
        ROLLBACK;
END;